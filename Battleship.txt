/*
--------Game of Battleship (v1.0.0-beta.2)--------
Creator: Davit
Discord: seltos7224

-== Changelog ==-
*/


  // Tile IDs
// 0 - restricted tile
// 1 - empty tile
// 2 - ship tile
// 3 - hit empty tile
// 4 - hit ship tile
// 5 - exploded ship tiles
// 6 - marker
// 10+ - ship tile preview (subtract 10 for the non-preview-equivalent tile value)

// ===================================
// SECTION Data
// ===================================

// > -------- <
// SECTION Vars
// > -------- <
  // ANCHOR Gameplay-related
var shipTileCount = 20  // Total ship tiles per board
var ships = [4, 3, 3, 2, 2, 2, 1, 1, 1, 1]  // All ships and sizes
var selectedShipSize = 4
var shipCounter  // Displays ships remaining of both sides
var directions = [  // Ship direction [y, x]
  [1, 0],  // vertical
  [0, 1]  // horizontal
]
var rotateButton  // Changes ship orientation
var rotateButtonText
var placeHorizontal = true  // True if the ship is going to be placed horizontally
var confirmShips  // Confirm placed ships button
var confirmShipsText
var ship4  // 4-tile ship
var ship3  // 3-tile ship
var ship2  // 2-tile ship
var ship1  // 1-tile ship
var ship4text  // Button text
var ship3text  // Button text
var ship2text  // Button text
var ship1text  // Button text & ascii
var ship2horizontal = ["#,#│\n▼▲▼▲", ">###\n▼▲▼#"]
var ship3horizontal = ["#,#╞\n▼▲▼▲", "╬#╤═\n▼▲▼▲", ">###\n▼▲▼#"]
var ship4horizontal = ["#┌o#\n▼▲▼▲", "_▄╞╬\n▼▲▼▲", "#┌☼♂\n▼▲▼▲", "☼┐##\n▼▲▼#"]
var ship2vertical = ["#^##\n/ \#", "│⌐φ•\n\_/#"]
var ship3vertical = ["#^##\n/ \#", "│ φ═\n│ │#", "\☼/#\n#¯##"]
var ship4vertical = ["#^##\n/ \#", "│ │#\n│ φ═", "│ φ═\n│ │#", "\☼/#\n#V##"]
var ship1textEnemy = "[│##\n▼▲▼#"
var ship2horizontalEnemy = ["##[│\n▼▲▼▲", "#,##\n▼▲▼#"]
var ship3horizontalEnemy = ["##{═\n▼▲▼▲", "╤#╞╪\n▼▲▼▲", "#,##\n▼▲▼#"]
var ship4horizontalEnemy = ["#┌☼λ\n▼▲▼▲", "☼┐╞╪\n▼▲▼▲", "#▄_ \n▼▲▼▲", "o┐##\n▼▲▼#"]
var ship2verticalEnemy = ["##^#\n#/ \\", "•α¬│\n#\_/"]
var ship3verticalEnemy = ["##^#\n#/ \\", "═α │\n#│ │", "#\☼/\n##¯#"]
var ship4verticalEnemy = ["##^#\n#/ \\", "#│ │\n═α │", "═α │\n#│ │", "#\☼/\n##V#"]
var previewedShipCoords = []  // Storing previewed ship coordinates
var placedShipCoords = []  // Storing placed ship coordinates
var placedShipCoordsFlattened = []  // Storing placed ship coordinates (flattened)
var playerWeaponAbsPos = [] // Coordinates of player weapons (offset to where the shot starts)
var placedEnemyShipCoords = []  // Storing placed enemy ship coordinates
var enemyWeaponAbsPos = [] // Coordinates of enemy weapons (offset to where the shot starts)
var playerMarkers = []  // Player marker list ([y, x, previous_ID])
var enemyMarkers = []  // Enemy marker list ([y, x, previous_ID])
var eHitY  // Y coordinate of the P tile that will be attacked
var eHitX  // X coordinate of the P tile that will be attacked
var eHitTile  // P tile that will be attacked
var eHitList = []  // List of tiles that the enemy hit (in order)
var prevEHitListCount = [eHitList.Count(), 0, 0]  // Tracks new attacks
var possibleHitList = []  // List of guessed coordinates for the next attack on a hit ship
var playerLastAttack = []  // Y and X coordinates of the last tile attacked by the player
var __randInt  // A random int used with possibleHitList
var flashList = []  // A list of items that will flash (red)
var flashListCopy = []  // A copy of flashListCopy for error handling
flashListCopy = MatrixCopy(flashList)

  // ANCHOR UI
var playButton  // Start the game
var playButtonText
var exitButton  // Exit to main menu, then exit loc
var fancyX = ascii
┌###┐
##x##
└###┘
asciiend
var easyModeButton
var mediumModeButton
var hardModeButton
var gamemode = 1  // gamemode 1 (default) - easy, 2 - medium, 3 - hard
var bgPanel  // Covers the background
var markerButton  // Marker mode toggle button
var markerBtnText
var markerMode = false  // Toggle marker mode
var playerGridPanel  // Player board
var enemyGridPanel  // Enemy board
var pTiles = []  // Player board tiles
var eTiles = []  // Enemy board tiles
var pButtons = []  // Player board buttons
var eButtons = []  // Enemy board buttons
var pButtonText = []  // Player board ascii
var eButtonText = []  // Enemy board ascii
var allowUpdates = false  // Allow updating _Tiles and _Buttons arrays
var pCoords  // Coordinate labels
var eCoords  // Coordinate labels
var coordText = ascii
##1·##2·##3·##4·##5·##6·##7·##8·##9·##10#
A
·
B
·
C
·
D
·
E
·
F
·
G
·
H
·
I
·
J
·
asciiend
var victoryPanel  // Victory/defeat pop-up
var victoryText
var generateVictoryPanel = -1  // Custom 3-value boolean (1 - do, 0 - don't, -1 - idle)
var replayButton  // Start a new game
var menuButton  // Exit to main menu
var confirmActionPanel  // "Confirm action?" pop-up
var confirmActionText
var confirmActionButton
var cancelActionButton
var infoPanel  // A panel that shows information about the game
var infoText
var infoButton  // infoPanel toggle button
var infoBtnText

  // ANCHOR Turns and score
var playerTurn = true  // Tracks turns
var turnCount = 1  // Counts turns for scoring
var pHitScore = 0
var eHitScore = 0
var pSinkScore = 0
var eSinkScore = 0
var score  // Total score

// > ---- <
// !SECTION
// > ---- <


// ANCHOR Add UI components
?loc = "Waterfall" & loc.begin
  // Play button
  playButton = AddUI(button, center_center, center_center, 30, 7, 20, -2)
  playButton.text = ""
  playButton.bcolor = "#ffffff"
  playButton.SetPressed(Start)
    // Play button text
  playButtonText = AddUI(text, center_center, center_center, 24, 5, playButton.x, playButton.y)
  playButtonText.anchor = center_center
  playButtonText.dock = center_center
  playButtonText.x = playButton.x
  playButtonText.y = playButton.y
  playButtonText.h = 5
  playButtonText.w = 24
  playButtonText.text = ascii
 ____  __     __   _  _ 
(  _ \(  )   / _\ ( \/ )
 ) __// (_/\/    \ )  / 
(__)  \____/\_/\_/(__/  
asciiend

  // Gamemode buttons
    // Easy
  easyModeButton = AddUI(button, center_center, center_center, 10, 3, 10, 3)
  easyModeButton.text = "Easy"
  easyModeButton.bcolor = "#00ff00"
  easyModeButton.tcolor = easyModeButton.bcolor
  easyModeButton.style = 3
  easyModeButton.SetPressed(SetMode)
    // Medium
  mediumModeButton = AddUI(button, center_center, center_center, 10, 3, 20, 3)
  mediumModeButton.text = "Medium"
  mediumModeButton.bcolor = "#ffff00"
  mediumModeButton.tcolor = mediumModeButton.bcolor
  mediumModeButton.SetPressed(SetMode)
    // Hard
  hardModeButton = AddUI(button, center_center, center_center, 10, 3, 30, 3)
  hardModeButton.text = "Hard"
  hardModeButton.bcolor = "#ff0000"
  hardModeButton.tcolor = hardModeButton.bcolor
  hardModeButton.SetPressed(SetMode)
  
  // Black background
  bgPanel = AddUI(panel, center_center, center_center, screen.w + 2, screen.w + 2, 0, 0)
  bgPanel.style = 0
  bgPanel.visible = false

  // Generate 10x10 boards
    // Player
  playerGridPanel = AddUI(panel, center_right, center_center, 42, 22, -1, 2)
  playerGridPanel.visible = false
  BoardReset(pButtons)
  BoardReset(pButtonText)
  for y = 0..9
    for x = 0..9
        // Buttons
      pButtons[y][x] = ui.AddButton()
      playerGridPanel.Add(pButtons[y][x])
      pButtons[y][x].anchor = top_left
      pButtons[y][x].dock = top_left
      pButtons[y][x].x = x * (playerGridPanel.w / 10) + 1
      pButtons[y][x].y = y * (playerGridPanel.h / 10) + 1
      pButtons[y][x].w = (playerGridPanel.w - 2) / 10
      pButtons[y][x].h = (playerGridPanel.h - 2) / 10
      pButtons[y][x].style = 0
      pButtons[y][x].text = "#"
        // Text
      pButtonText[y][x] = ui.AddText()
      playerGridPanel.Add(pButtonText[y][x])
      pButtonText[y][x].anchor = top_left
      pButtonText[y][x].dock = top_left
      pButtonText[y][x].x = x * (playerGridPanel.w / 10) + 1
      pButtonText[y][x].y = y * (playerGridPanel.h / 10) + 1
      pButtonText[y][x].w = (playerGridPanel.w - 2) / 10
      pButtonText[y][x].h = (playerGridPanel.h - 2) / 10
      pButtonText[y][x].text = "#"
      pButtonText[y][x].color = "#00aaff"
    // Enemy
  enemyGridPanel = AddUI(panel, center_left, center_center, 42, 22, 1, 2)
  enemyGridPanel.visible = false
  BoardReset(eButtons)
  BoardReset(eButtonText)
  for y = 0..9
    for x = 0..9
        // Button
      eButtons[y][x] = ui.AddButton()
      enemyGridPanel.Add(eButtons[y][x])
      eButtons[y][x].anchor = top_left
      eButtons[y][x].dock = top_left
      eButtons[y][x].x = x * (enemyGridPanel.w / 10) + 1
      eButtons[y][x].y = y * (enemyGridPanel.h / 10) + 1
      eButtons[y][x].w = (enemyGridPanel.w - 2) / 10
      eButtons[y][x].h = (enemyGridPanel.h - 2) / 10
      eButtons[y][x].style = 0
      eButtons[y][x].text = "#"
      eButtons[y][x].SetPressed(AddMarker)
        // Text
      eButtonText[y][x] = ui.AddText()
      enemyGridPanel.Add(eButtonText[y][x])
      eButtonText[y][x].anchor = top_left
      eButtonText[y][x].dock = top_left
      eButtonText[y][x].x = x * (enemyGridPanel.w / 10) + 1
      eButtonText[y][x].y = y * (enemyGridPanel.h / 10) + 1
      eButtonText[y][x].w = (enemyGridPanel.w - 2) / 10
      eButtonText[y][x].h = (enemyGridPanel.h - 2) / 10
      eButtonText[y][x].text = "#"
      eButtonText[y][x].color = "#00aaff"

    // Coordinates
      // Player
  pCoords = AddUI(text, top_left, top_left, playerGridPanel.w, playerGridPanel.h, 0, 0)
  playerGridPanel.Add(pCoords)
  pCoords.text = coordText
  pCoords.color = "#4c7589"
      // Enemy
  eCoords = AddUI(text, top_left, top_left, enemyGridPanel.w, enemyGridPanel.h, 0, 0)
  enemyGridPanel.Add(eCoords)
  eCoords.text = coordText
  eCoords.color = "#4c7589"

    // Remaining ships display
  shipCounter = AddUI(text, top_right, top_left, 8, 2, 3, -3)
  enemyGridPanel.Add(shipCounter)

    // Ship confirm button (player)
  confirmShips = AddUI(button, center_center, center_center, 43, 7, playerGridPanel.w + enemyGridPanel.x - playerGridPanel.x - 1, -8)
  playerGridPanel.Add(confirmShips)
    // Text on confirmShips
  confirmShipsText = AddUI(text, center_center, center_center, 40, 5, confirmShips.x + 1, confirmShips.y)
  playerGridPanel.Add(confirmShipsText)
  confirmShipsText.text = ascii
  ___  __   __ _  ____  __  ____  _  _ 
 / __)/  \ (  ( \(  __)(  )(  _ \( \/ )
( (__(  O )/    / ) _)  )(  )   // \/ \
 \___)\__/ \_)__)(__)  (__)(__\_)\_)(_/
asciiend
    // 4-tile ship
  ship4 = AddUI(button, center_right, center_center, 19, 4, confirmShips.x + 2, confirmShips.y + 7)
  playerGridPanel.Add(ship4)
  ship4.style = 2
  ship4.text = ""
  ship4.SetPressed(SelectShip)
    // ship4 text
  ship4text = AddUI(text, center_right, center_center, 16, 2, ship4.x - 1, ship4.y)
  playerGridPanel.Add(ship4text)
  ship4text.text = "#┌o#_▄╞╬#┌☼♂☼┐##\n▼▲▼▲▼▲▼▲▼▲▼▲▼▲▼ "
    // 3-tile ship
  ship3 = AddUI(button, center_left, center_center, 15, 4, confirmShips.x + 3, confirmShips.y + 7)
  playerGridPanel.Add(ship3)
  ship3.style = 2
  ship3.text = ""
  ship3.SetPressed(SelectShip)
    // ship3 text
  ship3text = AddUI(text, center_left, center_center, 12, 2, ship3.x + 2, ship3.y)
  playerGridPanel.Add(ship3text)
  ship3text.text = "#,#╞╬#╤═>###\n▼▲▼▲▼▲▼▲▼▲▼#"
    // 2-tile ship
  ship2 = AddUI(button, center_right, center_center, 11, 4, confirmShips.x + 2, confirmShips.y + 11)
  playerGridPanel.Add(ship2)
  ship2.style = 2
  ship2.text = ""
  ship2.SetPressed(SelectShip)
    // ship2 text
  ship2text = AddUI(text, center_right, center_center, 8, 2, ship2.x - 1, ship2.y)
  playerGridPanel.Add(ship2text)
  ship2text.text = "#,#│>###\n▼▲▼▲▼▲▼#"
    // 1-tile ship
  ship1 = AddUI(button, center_left, center_center, 7, 4, confirmShips.x + 3, confirmShips.y + 11)
  playerGridPanel.Add(ship1)
  ship1.style = 2
  ship1.text = ""
  ship1.SetPressed(SelectShip)
    // ship1 text
  ship1text = AddUI(text, center_left, center_center, 4, 2, ship1.x + 2, ship1.y)
  playerGridPanel.Add(ship1text)
  ship1text.text = "#│>#\n▼▲▼#"
    // Rotate ship button
  rotateButton = AddUI(button, center_center, center_center, 7, 3, confirmShips.x - confirmShips.w / 2 + 3, confirmShips.y + 16)
  playerGridPanel.Add(rotateButton)
  rotateButton.text = ""
  rotateButton.SetPressed(RotateShip)
    // Rotate button text
  rotateButtonText = AddUI(text, center_center, center_center, 7, 4, confirmShips.x - confirmShips.w / 2 + 3, confirmShips.y + 18)
  playerGridPanel.Add(rotateButtonText)
  rotateButtonText.text = "◀ ▶"
  rotateButtonText.align = center

  // Marker mode toggle button and text
    // Marker mode toggle button
  markerButton = AddUI(button, top_left, top_left, 5, 3, 0, 0)
  markerButton.text = "▼"
  markerButton.bcolor = "#e2e2e2"
  markerButton.tcolor = "#17ac5a"
  markerButton.style = 4
  markerButton.SetPressed(ToggleMarkerMode)
  markerButton.visible = false
    // Marker button text
  markerBtnText = AddUI(text, top_left, top_left, 5, 5, markerButton.x, markerButton.y)
  markerBtnText.text = fancyX
  markerBtnText.color = "#ff0000"
  markerBtnText.visible = false

  // Victory panel
    // Panel
  victoryPanel = AddUI(panel, center_center, center_center, 24, 6, 0, 0)
  victoryPanel.visible = false
    // Text
  victoryText = AddUI(text, center_center, center_center, 20, 2, 0, 0)
  victoryPanel.Add(victoryText)
  victoryText.align = center
    // Replay button
  replayButton = AddUI(button, center_left, center_center, 10, 3, 0, 2)
  victoryPanel.Add(replayButton)
  replayButton.style = 2
  replayButton.text = "Replay"
  replayButton.SetPressed(Start)
    // Exit to menu button
  menuButton = AddUI(button, center_right, center_center, 10, 3, 0, 2)
  victoryPanel.Add(menuButton)
  menuButton.style = 2
  menuButton.text = " Menu "
  menuButton.SetPressed(ExitToMenu)

  // Confirm action pop-up panel
    // Panel
  confirmActionPanel = AddUI(panel, center_center, center_center, 30, 9, 0, 0)
  confirmActionPanel.color = "#ff8000"
  confirmActionPanel.visible = false
    // Text
  confirmActionText = AddUI(text, center_center, center_center, 26, 1, 0, 0)
  confirmActionPanel.Add(confirmActionText)
  confirmActionText.text = "Exit to main menu?"
  confirmActionText.color = "#ff8000"
  confirmActionText.align = center
    // Confirm button
  confirmActionButton = AddUI(button, center_left, center_center, 14, 3, 0, 4)
  confirmActionPanel.Add(confirmActionButton)
  confirmActionButton.style = 2
  confirmActionButton.text = "Confirm "
  confirmActionButton.tcolor = "#ffff00"
  confirmActionButton.bcolor = "#ffff00"
  confirmActionButton.SetPressed(ExitToMenu)
    // Cancel button
  cancelActionButton = AddUI(button, center_right, center_center, 14, 3, 0, 4)
  confirmActionPanel.Add(cancelActionButton)
  cancelActionButton.style = 2
  cancelActionButton.text = "Cancel"
  cancelActionButton.tcolor = "#2fabea"
  cancelActionButton.bcolor = "#2fabea"
  cancelActionButton.SetPressed(CancelAction)

  // Exit button
  exitButton = AddUI(button, top_right, top_right, 4, 3, 0, 0)
  exitButton.text = "→]"
  exitButton.tcolor = "#ffff00"
  exitButton.style = 4

  // Interface
    // Info panel toggle button
  infoButton = AddUI(button, top_left, top_left, 5, 3, 0, 0)
  infoButton.text = "i"
  infoButton.SetPressed(TogglePanel)
    // Info button text
  infoBtnText = AddUI(text, top_left, top_left, 5, 5, infoButton.x, infoButton.y)
  infoBtnText.text = fancyX
  infoBtnText.color = "#ff0000"
  infoBtnText.visible = false
    // Info panel
  infoPanel = AddUI(panel, center_center, center_center, screen.w - 22, screen.h - 4, 0, 0)
  infoPanel.visible = false
    // Info panel text
  infoText = AddUI(text, center_center, center_center, infoPanel.w - 6, infoPanel.h - 4, 0, 0)
  infoPanel.Add(infoText)
  infoText.align = center
  infoText.text = "[color=#3ba2d5]Welcome To Battleship![/color]\n" +
    ^"By Davit\n" + 
    ^"\n" +
    ^"[color=#c89e29]How to Play[/color]\n" + 
    ^"You have 10 ships in total — 4x 1-tile, 3x 2-tile, 2x 3-tile, and 1x 4-tile ships. Place them on your board to start the game. Press Shift or [◀ ▶] to change ship orientation. Ships cannot touch horizontally, vertically or diagonally. The game is turn-based. Hitting an enemy ship grants you an extra move. Sink all enemy ships to win!\n" + 
    // ^"\n" + 
    ^"[color=#c89e29]Marker Mode[/color]\n" + 
    ^"Press Ctrl or the [[color=#17ac5a]▼[/color]] button to add markers to tiles. When in marker mode, your board will have a green border.\n" + 
    // ^"\n" + 
    ^"[color=#c89e29]Score[/color]\n" + 
    ^"The highest possible score is 100 points.\n" + 
    ^"\n" +
    ^"[color=#3ba2d5]Smooth seas to ye![/color]"

// ===================================
// !SECTION
// ===================================


// ===================================
// SECTION Gameplay
// ===================================

// ANCHOR Marker mode
?key = "ability2Begin"  // "Ctrl"
  ToggleMarkerMode()
?markerMode = true
  playerGridPanel.color = "#429367" //"#17ac5a"
  enemyGridPanel.color = "#429367" //"#17ac5a"
  pCoords.color = "#429367"
  eCoords.color = "#429367"
  markerButton.style = 0
  markerButton.text = "x"
  markerBtnText.visible = true
:
  playerGridPanel.color = "#4c7589" //"#00aaff"
  enemyGridPanel.color = "#4c7589" //"#00aaff"
  pCoords.color = "#4c7589"
  eCoords.color = "#4c7589"
  markerButton.style = 1
  markerButton.text = "▼"
  markerBtnText.visible = false

// Rotate ships with a keyboard shortcut
?rotateButton.visible & key = "ability1Begin"  // "Shift"
  RotateShip()

// Tracks new enemy hits
prevEHitListCount[2] = prevEHitListCount[1] // delay
?prevEHitListCount[0] ! eHitList.Count()
  prevEHitListCount[1] = prevEHitListCount[0]
  prevEHitListCount[0] = eHitList.Count()

// ANCHOR Delay between turns and shooting animations
var turnDelay = 0
var defaultTurnDelay = 30
var delayTime = 0
var delayTimeCaptured = false
var isEnemyAttack
//
var shotCanvas = AddUI("canvas", "top_left", "top_left", playerGridPanel.w + enemyGridPanel.w + 2, playerGridPanel.h, playerGridPanel.absoluteX, playerGridPanel.absoluteY) //TODO move line up
//
?turnDelay >= 0
  ?!delayTimeCaptured
    delayTime = turnDelay
    delayTimeCaptured = true

  shotCanvas.Set("#")

  ?pButtons[0][0].visible
    for y = 0..9
      for x = 0..9
        pButtons[y][x].visible = false
        eButtons[y][x].visible = false
  :?turnDelay = 0
    for y = 0..9
      for x = 0..9
        pButtons[y][x].visible = "inherit"
        eButtons[y][x].visible = "inherit"
    allowUpdates = true
    delayTimeCaptured = false
    isEnemyAttack = false
  :
    var blockUp = "▀"
    var blockDown = "▄"
    var lineUp = "‾"
    var lineMid = "—"
    var lineDown = "_"

    var randWeapon
    var weaponType
    var startY
    var startX
    var endY
    var endX
    var distY
    var distX
    var pathLength
    var dirY
    var dirX
    var projY
    var projX
    var projSpeed
    var setCanvasY
    var setCanvasX
    //FIXME shot anim
    ?turnDelay >= delayTime - 1
      ?prevEHitListCount[1] ! prevEHitListCount[2]
        isEnemyAttack = true
      ?isEnemyAttack
        randWeapon = enemyWeaponAbsPos[RandIntInRange(0, enemyWeaponAbsPos.Count()-1)]
      :
        randWeapon = playerWeaponAbsPos[RandIntInRange(0, playerWeaponAbsPos.Count()-1)]
      startY = randWeapon[0]
      startX = randWeapon[1]
      endY = eHitY
      endX = eHitX
      distY = endY - startY
      distX = endX - startX
      pathLength = math.Sqrt(distY * distY  +  distX * distX)
      dirY = math.RoundToInt(distY / pathLength)
      dirX = math.RoundToInt(distX / pathLength)
      projY = startY
      projX = startX
      projSpeed = math.CeilToInt(delayTime / pathLength)
      weaponType = randWeapon[2]

    ?Type(isEnemyAttack) = "bool"
      // ?!isEnemyAttack
        // playerLastAttack //
        // eButtons[playerLastAttack[0]][playerLastAttack[1]] //
        projY += dirY * projSpeed
        projX += dirX * projSpeed
        setCanvasY = projY - shotCanvas.y
        setCanvasX = projX - shotCanvas.x
        ?math.Round(projY) > math.Floor(projY)
          shotCanvas.Set(setCanvasX, setCanvasY, "#red", blockUp)
        :
          shotCanvas.Set(setCanvasX, setCanvasY, "#red", blockDown)
      // :
        // eHitY, eHitX //
        // eHitTile? ebuttons[ehy][ehx]? //
  turnDelay --
  >@input.x@ - @startX@
  >`0,1,@input.y@ - @startY@

// ANCHOR Enemy turn
:?!playerTurn
  // Reset eHitTile and possibleHitList
  eHitTile = -1
  possibleHitList.Clear()

  // Choose a tile to hit
  ?eHitList.Count() = 0
    eHitY = RandIntInRange(0, 9)
    eHitX = RandIntInRange(0, 9)
    var rerolleHitTiles = false
    for i = 0..99
      ?gamemode >= 3
        for sxi : [-1, 0, 1]
          for syi : [-1, 0, 1]
            ?eHitY + syi < 0 | eHitX + sxi < 0 | eHitY + syi > 9 | eHitX + sxi > 9
              continue
            ?!(sxi = 0 & syi = 0)
              ?pTiles[eHitY + syi][eHitX + sxi] = 5
              ^| (pTiles[eHitY + syi][eHitX + sxi] = 4 & math.Abs(syi) = math.Abs(sxi))
                rerolleHitTiles = true
                break
          ?rerolleHitTiles = true
            break
      ?![0, 1, 2, 6].Contains(pTiles[eHitY][eHitX]) | rerolleHitTiles
        eHitY = RandIntInRange(0, 9)
        eHitX = RandIntInRange(0, 9)
        rerolleHitTiles = false
      :
        break
    eHitTile = pTiles[eHitY][eHitX]
  :
    // Choose a direction to expand
    ?eHitList.Count() > 1
      // Find next tile to hit
      var firstY
      var firstX
      var lastY
      var lastX
      ?eHitList[eHitList.Count()-1][0] > eHitList[0][0]
        firstY = eHitList[0][0]
        lastY = eHitList[eHitList.Count()-1][0]
        firstX = eHitList[0][1]
        lastX = firstX
      :?eHitList[eHitList.Count()-1][0] < eHitList[0][0]
        firstY = eHitList[eHitList.Count()-1][0]
        lastY = eHitList[0][0]
        firstX = eHitList[0][1]
        lastX = firstX
      :?eHitList[eHitList.Count()-1][1] > eHitList[0][1]
        firstX = eHitList[0][1]
        lastX = eHitList[eHitList.Count()-1][1]
        firstY = eHitList[0][0]
        lastY = firstY
      :?eHitList[eHitList.Count()-1][1] < eHitList[0][1]
        firstX = eHitList[eHitList.Count()-1][1]
        lastX = eHitList[0][1]
        firstY = eHitList[0][0]
        lastY = firstY

      // Optimize up or down / left or right choice
      var rerolleHitTiles1 = false
      var rerolleHitTiles3 = false
      var tryY
      var tryX
      ?firstX = lastX
        ?firstY = 0
          tryY = lastY + 1
        :
          tryY = firstY - 1
        tryX = firstX
      :
        tryY = firstY
        ?firstX = 0
          tryX = lastY + 1
        :
          tryX = firstX - 1
      ?tryX <= 9 & tryX >= 0 & tryY <= 9 & tryY >= 0
        for sxi : [-1, 0, 1]
          for syi : [-1, 0, 1]
            ?tryY + syi < 0 | tryX + sxi < 0 | tryY + syi > 9 | tryX + sxi > 9
              continue
            ?!(sxi = 0 & syi = 0)
              ?pTiles[tryY + syi][tryX + sxi] = 5
              ^| (pTiles[tryY + syi][tryX + sxi] = 4 & math.Abs(syi) = math.Abs(sxi))
                rerolleHitTiles1 = true
                break
          ?rerolleHitTiles1
            break
      :
        rerolleHitTiles3 = true
      
      // Vertical ship
      ?firstX = lastX
        // Up (if not, down)
        ?firstY - 1 >= 0 & [0, 1, 2, 6].Contains(pTiles[firstY - 1][firstX]) & (gamemode < 3 | !rerolleHitTiles1) & !rerolleHitTiles3
          possibleHitList.Add([firstY - 1, firstX])
        :?lastY + 1 < 9
          possibleHitList.Add([lastY + 1, firstX])
          rerolleHitTiles1 = false
        :
          possibleHitList.Clear()
          rerolleHitTiles1 = false
          rerolleHitTiles3 = false
      // Horizontal ship
      :?firstY = lastY
        // Left (if not, right)
        ?firstX - 1 >= 0 & [0, 1, 2, 6].Contains(pTiles[firstY][firstX - 1]) & (gamemode < 3 | !rerolleHitTiles1) & !rerolleHitTiles3
          possibleHitList.Add([firstY, firstX - 1])
        :?lastX + 1 < 9
          possibleHitList.Add([firstY, lastX + 1])
          rerolleHitTiles1 = false
        :
          possibleHitList.Clear()
          rerolleHitTiles1 = false
          rerolleHitTiles3 = false
      
      // If there is at least one extension option
      ?possibleHitList.Count() > 0
        __randInt = RandIntInRange(0, possibleHitList.Count()-1)
        ?gamemode >= 2 | RandIntInRange(1, 100) <= 12
          eHitY = possibleHitList[__randInt][0]
          eHitX = possibleHitList[__randInt][1]
        :
          eHitY = RandIntInRange(0, 9)
          eHitX = RandIntInRange(0, 9)
          for i = 0..99
            ?![0, 1, 2, 6].Contains(pTiles[eHitY][eHitX])
              eHitY = RandIntInRange(0, 9)
              eHitX = RandIntInRange(0, 9)
            :
              break
      : // No extensions, make a random attack
        eHitY = RandIntInRange(0, 9)
        eHitX = RandIntInRange(0, 9)
        for i = 0..99
          var rerolleHitTiles2 = false
          ?gamemode >= 3
            for sxi : [-1, 0, 1]
              for syi : [-1, 0, 1]
                ?eHitY + syi < 0 | eHitX + sxi < 0 | eHitY + syi > 9 | eHitX + sxi > 9
                  continue
                ?!(sxi = 0 & syi = 0)
                  ?pTiles[eHitY + syi][eHitX + sxi] = 5
                  ^| (pTiles[eHitY + syi][eHitX + sxi] = 4 & math.Abs(syi) = math.Abs(sxi))
                    rerolleHitTiles2 = true
                    break
              ?rerolleHitTiles2 = true
                break
          ?![0, 1, 2, 6].Contains(pTiles[eHitY][eHitX]) | rerolleHitTiles2
            eHitY = RandIntInRange(0, 9)
            eHitX = RandIntInRange(0, 9)
            rerolleHitTiles2 = false
          :
            break
      eHitTile = pTiles[eHitY][eHitX]
    :
      for syi : [-1, 0, 1]
        for sxi : [-1, 0, 1]
          var hitY = eHitList[0][0]
          var hitX = eHitList[0][1]
          ?hitY + syi < 0 | hitX + sxi < 0 | hitY + syi > 9 | hitX + sxi > 9
            continue
          ?math.Abs(sxi) ! math.Abs(syi)
            ?([0, 1, 2, 6].Contains(pTiles[hitY + syi][hitX + sxi]))
              possibleHitList.Add([hitY + syi, hitX + sxi])
      __randInt = RandIntInRange(0, possibleHitList.Count()-1)
      eHitY = possibleHitList[__randInt][0]
      eHitX = possibleHitList[__randInt][1]
      eHitTile = pTiles[eHitY][eHitX]

  // Apply the attack
  ?[0, 1, 6].Contains(eHitTile)  // (The shot hits water.)
    pTiles[eHitY][eHitX] = 3
    turnDelay = defaultTurnDelay
    playerTurn = true
    turnCount ++
  :?eHitTile = 2  // (The shot hits a ship. Continue the turn.)
    pTiles[eHitY][eHitX] = 4
    eHitList.Add([eHitY, eHitX])
    turnDelay = defaultTurnDelay
    KillShip(pTiles, eHitY, eHitX)
    eHitScore ++
  // :  // (Insurance. If eHitTile is in [3, 4, 5], skip turn to escape error.)
  //   playerTurn = true
  //   turnCount ++

// ANCHOR Ship counter
var pSunk = [pSinkScore, 10]
var eSunk = [eSinkScore, 10]
?pSunk[0] ! pSinkScore
  pSunk[0] = pSinkScore
  pSunk[1] -= 1
?eSunk[0] ! eSinkScore
  eSunk[0] = eSinkScore
  eSunk[1] -= 1
?!enemyGridPanel.visible
  pSunk = [pSinkScore, 10]
  eSunk = [eSinkScore, 10]
var spaceFiller = ""
?eSunk[1] < 10
  spaceFiller = " "
:
  spaceFiller = ""
shipCounter.text = "[color=#999999]#Turn " + turnCount + "[/color]\n[color=#3ba2d5]" + spaceFiller + eSunk[1] + "[/color]" + "[color=#999999] || [/color]" + "[color=#ffa800]" + pSunk[1] + "[/color]"

// ANCHOR ShipWasNotSelectedError
// Throw an error when a ship is not selected while placing them (player)
var selectionErrorMessage = "Please select a ship."
var selectionErrTimer = 90
?confirmShips.visible = true & selectionErrTimer < 90
  >`@screen.w / 2 - string.Size(selectionErrorMessage) / 2@,1,#red,@selectionErrorMessage@
  selectionErrTimer ++

// ANCHOR Confirm ships
// If the player has placed all 20 ship tiles, "Confirm" will start the game
var printError = false
var shipErrorMessage = "Place all your ships before starting the game."
var boolSwitchTimer = 90
?confirmShips.visible = true
  ?placedShipCoordsFlattened.Count() = 20
    confirmShips.SetPressed(StartGameplay)
    printError = false
  :
    confirmShips.SetPressed(ResetErrorTimer)
    printError = true
  ?printError = true
    ?boolSwitchTimer < 90
      >`@screen.w / 2 - string.Size(shipErrorMessage) / 2@,1,#red,@shipErrorMessage@
      boolSwitchTimer ++

// ANCHOR Calculate score
var gamemodeModifier = [0.55, 0.7, 0.85, 1]  // easy, medium, hard, expert
score = math.RoundToInt(math.Max(0, ((pHitScore + pSinkScore) * 70 / 40) - ((eHitScore + eSinkScore) * 40 / 40) + (math.Max(0, math.Floor(31 - turnCount * 0.5)) * 30 / 30)) * gamemodeModifier[gamemode - 1])
  // Decrease turnCount multiplier (k) softer punishment. k = 0.6 yields bonus pts up to turn 50 (30/k turns).
  // Soft cap formula (harsher punishment for letting the enemy hit you at the beginning, then it evens out at the end. p (power) = 0.8 yields max punishment of 50.5 points) -> "...- ((eHitScore + eSinkScore) ^ 0.8 * 70 / 40) +..."
  // Raw score range: [0, 70]; Adapted: [0, 100]

// ANCHOR Victory check
?pHitScore = 20
  for y = 0..9
    for x = 0..9
      pButtons[y][x].visible = false
      eButtons[y][x].visible = false
  victoryPanel.visible = true
  victoryPanel.color = "#17ac5a"
  replayButton.bcolor = "#ffff00"
  replayButton.tcolor = "#ffff00"
  menuButton.bcolor = "#2fabea"
  menuButton.tcolor = "#2fabea"
  victoryText.color = victoryPanel.color
  victoryText.text = "You Won!\nScore:  " + score
:?eHitScore > 0 & eHitScore = placedShipCoordsFlattened.Count()
  for y = 0..9
    for x = 0..9
      pButtons[y][x].visible = false
      eButtons[y][x].visible = false
  victoryPanel.visible = true
  victoryPanel.color = "#ff0000"
  replayButton.bcolor = "#ffff00"
  replayButton.tcolor = "#ffff00"
  menuButton.bcolor = "#2fabea"
  menuButton.tcolor = "#2fabea"
  victoryText.color = victoryPanel.color
  victoryText.text = "You Lost!\nScore: " + score


// ANCHOR Most key shortcuts
// In a pop-up, "<-" & "->" (or "A" & "D") change selected button, "C" confirms, "X" cancels
  // Cooldown for the "X" key, preventing double commands
var exitShortcutCooldown = 0
var default_cd_5f = 5
?exitShortcutCooldown > 0
  exitShortcutCooldown --

  // Victory panel pop-up
?victoryPanel.visible
  ?key = "backEnd"  // "X"
    exitShortcutCooldown = default_cd_5f
    ExitToMenu(menuButton)
  :?key = "leftEnd"  // "<-" / "A"
    ?menuButton.style = 2
      menuButton.style = 3
      replayButton.style = 2
    :
      menuButton.style = 2
      replayButton.style = 3
  :?key = "rightEnd"  // "->" / "D"
    ?replayButton.style = 2
      replayButton.style = 3
      menuButton.style = 2
    :
      replayButton.style = 2
      menuButton.style = 3
  :?key = "bumpREnd"  // "C"
    ?menuButton.style = 3
      ExitToMenu(menuButton)
    :
      Start()
:
  replayButton.style = 2
  menuButton.style = 2

  // ConfirmAction panel pop-up
?confirmActionPanel.visible
  ?key = "backEnd"  // "X"
    exitShortcutCooldown = default_cd_5f
    CancelAction(cancelActionButton)
  :?key = "leftEnd"  // "<-" / "A"
    ?cancelActionButton.style = 2
      cancelActionButton.style = 3
      confirmActionButton.style = 2
    :
      cancelActionButton.style = 2
      confirmActionButton.style = 3
  :?key = "rightEnd"  // "->" / "D"
    ?confirmActionButton.style = 2
      confirmActionButton.style = 3
      cancelActionButton.style = 2
    :
      confirmActionButton.style = 2
      cancelActionButton.style = 3
  :?key = "bumpREnd"  // "C"
    ?cancelActionButton.style = 3
      CancelAction(cancelActionButton)
    :
      ExitToMenu(confirmActionButton)
:
  confirmActionButton.style = 2
  cancelActionButton.style = 2

  // Info panel pop-up
?infoPanel.visible
  ?key = "backEnd"  // "X"
    exitShortcutCooldown = default_cd_5f
    infoPanel.visible = false

  // Exit button
?!infoPanel.visible & !victoryPanel.visible & !confirmActionPanel.visible & key = "backEnd" & exitShortcutCooldown = 0
  ?playerGridPanel.visible
    ExitToMenu(exitButton)
  :
    loc.Leave()

  // Cycle through ships
?confirmShips.visible & pButtons[0][0].visible
  ?key = "leftEnd"  // "<-" / "A"
    selectedShipSize ++
    for i = 1..4
      ?selectedShipSize > 4
        selectedShipSize = 1
      ?placedShips[selectedShipSize - 1] = 5 - selectedShipSize
        selectedShipSize ++
      :
        break
  :?key = "rightEnd"  // "->" / "D"
    selectedShipSize --
    for i = 1..4
      ?selectedShipSize < 1
        selectedShipSize = 4
      ?placedShips[selectedShipSize - 1] = 5 - selectedShipSize
        selectedShipSize --
      :
        break


// ANCHOR Info window
?!bgPanel.visible
  ?infoPanel.visible
    playButton.visible = false
    playButtonText.visible = false
    easyModeButton.visible = false
    mediumModeButton.visible = false
    hardModeButton.visible = false
    infoButton.style = 0
    infoButton.text = "x"
    infoBtnText.visible = true
  :
    playButton.visible = true
    playButtonText.visible = true
    easyModeButton.visible = true
    mediumModeButton.visible = true
    hardModeButton.visible = true
    infoButton.style = 1
    infoButton.text = "i"
    infoBtnText.visible = false

// ANCHOR Set exitButton function
?!playButton.visible & !infoPanel.visible
  exitButton.SetPressed(ExitToMenu)
:
  exitButton.SetPressed(loc.Leave)

// ANCHOR Enable/disable ship choice buttons
// Track the type of placed ships, disable buttons if all ships of that size are placed
var placedShips = [0, 0, 0, 0]  // 1, 2, 3, 4 tiles in order
?placedShips[0] = 4
  ship1.SetPressed(Nothing)
  ship1.tcolor = "#555577"
  ship1.bcolor = ship1.tcolor
  ship1text.color = ship1.tcolor
  ?selectedShipSize = 1
    selectedShipSize = 0
:
  ship1.SetPressed(SelectShip)
  ship1.tcolor = "#ffffff"
  ship1.bcolor = ship1.tcolor
  ship1text.color = ship1.tcolor
?placedShips[1] = 3
  ship2.SetPressed(Nothing)
  ship2.tcolor = "#555577"
  ship2.bcolor = ship2.tcolor
  ship2text.color = ship2.tcolor
  ?selectedShipSize = 2
    selectedShipSize = 0
:
  ship2.SetPressed(SelectShip)
  ship2.tcolor = "#ffffff"
  ship2.bcolor = ship2.tcolor
  ship2text.color = ship2.tcolor
?placedShips[2] = 2
  ship3.SetPressed(Nothing)
  ship3.tcolor = "#555577"
  ship3.bcolor = ship3.tcolor
  ship3text.color = ship3.tcolor
  ?selectedShipSize = 3
    selectedShipSize = 0
:
  ship3.SetPressed(SelectShip)
  ship3.tcolor = "#ffffff"
  ship3.bcolor = ship3.tcolor
  ship3text.color = ship3.tcolor
?placedShips[3] = 1
  ship4.SetPressed(Nothing)
  ship4.tcolor = "#555577"
  ship4.bcolor = ship4.tcolor
  ship4text.color = ship4.tcolor
  ?selectedShipSize = 4
    selectedShipSize = 0
:
  ship4.SetPressed(SelectShip)
  ship4.tcolor = "#ffffff"
  ship4.bcolor = ship4.tcolor
  ship4text.color = ship4.tcolor

?selectedShipSize = 0
  for i = 1..4
    ?selectedShipSize < 1
      selectedShipSize = 4
    ?placedShips[selectedShipSize - 1] = 5 - selectedShipSize
      selectedShipSize --
    :
      break

?selectedShipSize = 1
  ship1.style = 3
  ship2.style = 1
  ship3.style = 1
  ship4.style = 1
:?selectedShipSize = 2
  ship1.style = 1
  ship2.style = 3
  ship3.style = 1
  ship4.style = 1
:?selectedShipSize = 3
  ship1.style = 1
  ship2.style = 1
  ship3.style = 3
  ship4.style = 1
:?selectedShipSize = 4
  ship1.style = 1
  ship2.style = 1
  ship3.style = 1
  ship4.style = 3
:
  ship1.style = 1
  ship2.style = 1
  ship3.style = 1
  ship4.style = 1

// ANCHOR ConfirmPreviewedShipAlert
// Prints "Press on the previewed ship to confirm its placement."
var printPreviewInstructions = false
var dontPrintPreviewInstructions = false
var previewInstructionMessage = "Press on the previewed ship to confirm its placement."
?printPreviewInstructions = true
  >`@screen.w / 2 - string.Size(previewInstructionMessage) / 2@,2,#c523c5,@previewInstructionMessage@

// ===================================
// !SECTION
// ===================================


// ===================================
// SECTION Functions
// ===================================

// ANCHOR Set gamemode
func SetMode (btn)
  ?btn = easyModeButton
    gamemode = 1
    easyModeButton.style = 3
    mediumModeButton.style = 1
    hardModeButton.style = 1
    // expertModeButton.style = 1
  :?btn = mediumModeButton
    gamemode = 2
    easyModeButton.style = 1
    mediumModeButton.style = 3
    hardModeButton.style = 1
    // expertModeButton.style = 1
  :?btn = hardModeButton
    gamemode = 3
    easyModeButton.style = 1
    mediumModeButton.style = 1
    hardModeButton.style = 3
    // expertModeButton.style = 1
  // :?btn = expertModeButton
  //   gamemode = 4
  //   easyModeButton.style = 1
  //   mediumModeButton.style = 1
  //   hardModeButton.style = 1
  //   expertModeButton.style = 3

// ANCHOR Start the game
func Start ()
  // Reset score
  pHitScore = 0
  eHitScore = 0
  pSinkScore = 0
  eSinkScore = 0
  turnCount = 1

  // UI
  playButton.visible = false
  playButtonText.visible = false
  easyModeButton.visible = false
  mediumModeButton.visible = false
  hardModeButton.visible = false
  victoryPanel.visible = false
  infoButton.visible = false
  infoPanel.visible = false
  selectedShipSize = 4  // default
  placedShips = [0, 0, 0, 0]
  ship4.tcolor = "#ffffff"
  ship4.SetPressed(SelectShip)
  ship3.tcolor = "#ffffff"
  ship3.SetPressed(SelectShip)
  ship2.tcolor = "#ffffff"
  ship2.SetPressed(SelectShip)
  ship1.tcolor = "#ffffff"
  ship1.SetPressed(SelectShip)
  
  // Background
  ?!playButton.visible
    bgPanel.visible = true
  
  // Fill in the boards, reset arrays
  BoardReset(pTiles)
  BoardReset(eTiles)
  for y = 0..9
    for x = 0..9
      pButtons[y][x].visible = "inherit"
  placedShipCoords.Clear()
  placedShipCoordsFlattened.Clear()
  placedEnemyShipCoords.Clear()
  eHitList.Clear()
  playerWeaponAbsPos.Clear()
  enemyWeaponAbsPos.Clear()
  
  // Generate boards
  GenerateMap()

  // Allow array updates
  allowUpdates = true

// ANCHOR Exit to Main Menu
func ExitToMenu (btn)
  // If exiting mid-game, confirm action
  ?btn = exitButton
    confirmActionPanel.visible = true
    for y = 0..9
      for x = 0..9
        pButtons[y][x].visible = false
        ?enemyGridPanel.visible
          eButtons[y][x].visible = false
    return

  // Reset score
  pHitScore = 0
  eHitScore = 0
  pSinkScore = 0
  eSinkScore = 0
  turnCount = 1

  // UI
  playButton.visible = true
  playButtonText.visible = true
  easyModeButton.visible = true
  mediumModeButton.visible = true
  hardModeButton.visible = true
  victoryPanel.visible = false
  confirmActionPanel.visible = false
  confirmShips.visible = false
  confirmShipsText.visible = false
  ship4.visible = false
  ship3.visible = false
  ship2.visible = false
  ship1.visible = false
  ship4text.visible = false
  ship3text.visible = false
  ship2text.visible = false
  ship1text.visible = false
  rotateButton.visible = false
  ?!placeHorizontal
    RotateShip()
  markerButton.visible = false
  markerMode = false
  infoButton.visible = true
  printPreviewInstructions = false
  
  // Background
  bgPanel.visible = false

  // Boards
  playerGridPanel.visible = false
  enemyGridPanel.visible = false

  // Enable array updates
  allowUpdates = false

// ANCHOR Cancel an action from a pop-up
func CancelAction (btn)
  // Hide the pop-up
  btn.parent.visible = false
  
  // Enable player and enemy buttons
  ?playerGridPanel.visible & !pButtons[0][0].visible
    for y = 0..9
      for x = 0..9
        pButtons[y][x].visible = "inherit"
        ?enemyGridPanel.visible
          eButtons[y][x].visible = "inherit"

// ANCHOR Generate boards
func GenerateMap ()
  // Show/hide boards
  playerGridPanel.visible = true
  for y = 0..9
    for x = 0..9
      pButtons[y][x].SetPressed(AddShips)
  enemyGridPanel.visible = false
  confirmShips.visible = true
  confirmShipsText.visible = true
  ship4.visible = true
  ship3.visible = true
  ship2.visible = true
  ship1.visible = true
  ship4text.visible = true
  ship3text.visible = true
  ship2text.visible = true
  ship1text.visible = true
  rotateButton.visible = true
  
  // Generate ships (enemy)
  BoardReset(eTiles)
  for i : ships
    GenerateShips(i)
  BoardReset(pTiles)

  // Pinpoint enemy ship weapons
  for ships : placedEnemyShipCoords
    ?ships.Count() > 1
      var yAbsPos
      var xAbsPos
      var wepType
      // Horizontal ships
      ?ships[0][0] = ships[1][0]
        ?ships.Count() = 3
          yAbsPos = eButtons[ships[0][0]][ships[0][1]].absoluteY
          xAbsPos = eButtons[ships[0][0]][ships[0][1]].absoluteX + 1
          wepType = "ballista"
        :?ships.Count() = 4
          yAbsPos = eButtons[ships[0][0]][ships[0][1]].absoluteY - 1
          xAbsPos = eButtons[ships[0][0]][ships[0][1]].absoluteX + 2
          wepType = "catapult"
        :
          continue
        enemyWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])
      // Vertical ships
      :
        ?[2, 3].Contains(ships.Count()) // ships.Count() = 2
          yAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteY
          xAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteX - 1
          wepType = "cannon"
        // :?ships.Count() = 3
        //   yAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteY
        //   xAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteX - 1
        //   wepType = "cannon"
        :?ships.Count() = 4
          yAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteY + 1
          xAbsPos = eButtons[ships[1][0]][ships[1][1]].absoluteX - 1
          wepType = "cannon"
          enemyWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])
          yAbsPos = eButtons[ships[2][0]][ships[2][1]].absoluteY
        :
          continue
        enemyWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])

// ANCHOR Generate ships (enemy)
func GenerateShips (shipSize)
  // Add a ghost board of eTiles
  var ghostTiles = BoardCopy(eTiles)
  
  // Detected existing ship tiles
  var ghostShipYX = []

  // Ship direction
  var dir = directions[RandIntInRange(0, 1)]
  var dY = dir[0]
  var dX = dir[1]

  // Tiles
  var firstTileY = RandIntInRange(0, 9)
  var firstTileX = RandIntInRange(0, 9)
  for i = 1..20
    ?ghostTiles[firstTileY][firstTileX] ! 1
      firstTileY = RandIntInRange(0, 9)
      firstTileX = RandIntInRange(0, 9)
    :
      break
  var tile2Y = firstTileY + (1 * dY)
  var tile2X = firstTileX + (1 * dX)
  var tile3Y = firstTileY + (2 * dY)
  var tile3X = firstTileX + (2 * dX)
  var tile4Y = firstTileY + (3 * dY)
  var tile4X = firstTileX + (3 * dX)
  var tileListY = [firstTileY, tile2Y, tile3Y, tile4Y]
  var tileListX = [firstTileX, tile2X, tile3X, tile4X]
  for yx = 1..50
    ?shipSize = 1
      ghostTiles[firstTileY][firstTileX] = 2
      ghostShipYX.Add([firstTileY, firstTileX])
      placedEnemyShipCoords.Add([[firstTileY, firstTileX]])
      break
    for t = 0..shipSize-1
      ?tileListY[t] > 9 | tileListX[t] > 9 | ghostTiles[tileListY[t]][tileListX[t]] ! 1
        ghostShipYX.Clear()
        ?tileListY[t] > 9
          firstTileY = RandIntInRange(0, 10 - shipSize)
          firstTileX = RandIntInRange(0, 9)
        :?tileListX[t] > 9
          firstTileY = RandIntInRange(0, 9)
          firstTileX = RandIntInRange(0, 10 - shipSize)
        :
          firstTileY = RandIntInRange(0, 9)
          firstTileX = RandIntInRange(0, 9)
        tile2Y = firstTileY + (1 * dY)
        tile2X = firstTileX + (1 * dX)
        tile3Y = firstTileY + (2 * dY)
        tile3X = firstTileX + (2 * dX)
        tile4Y = firstTileY + (3 * dY)
        tile4X = firstTileX + (3 * dX)
        tileListY = [firstTileY, tile2Y, tile3Y, tile4Y]
        tileListX = [firstTileX, tile2X, tile3X, tile4X]
        break
      ghostShipYX.Add([tileListY[t], tileListX[t]])
    ?ghostShipYX.Count() = shipSize
      placedEnemyShipCoords.Add([])
      for i = 0..ghostShipYX.Count()-1
        ghostTiles[ghostShipYX[i][0]][ghostShipYX[i][1]] = 2
        // Copy placed enemy ship coordinates
        placedEnemyShipCoords[placedEnemyShipCoords.Count()-1].Add(ghostShipYX[i])
      break
  
  // Restrict adjacent tiles
  for y = 0..9
    for x = 0..9
      ?ghostTiles[y][x] = 2
        var sx = [-1, 0, 1]
        var sy = [-1, 0, 1]
        for sxi : sx
          for syi : sy
            ?y + syi < 0 | x + sxi < 0 | y + syi > 9 | x + sxi > 9
              continue
            ?!(sxi = 0 & syi = 0)
              ?ghostTiles[y + syi][x + sxi] = 1
                ghostTiles[y + syi][x + sxi] = 0
  
  // Assign ghost tiles to eTiles
  for y = 0..9
    for x = 0..9
      eTiles[y][x] = ghostTiles[y][x]

// ANCHOR Select ship (by size)
func SelectShip (btn)
  ?btn = ship4
    selectedShipSize = 4
  :?btn = ship3
    selectedShipSize = 3
  :?btn = ship2
    selectedShipSize = 2
  :?btn = ship1
    selectedShipSize = 1

// ANCHOR Rotate ship
func RotateShip ()
  placeHorizontal = !placeHorizontal
  ?placeHorizontal
    rotateButtonText.text = "◀ ▶"
    rotateButtonText.y = confirmShips.y + 18
    rotateButton.w = 7
    rotateButton.h = 3
  :
    rotateButtonText.text = ascii
▲
▼
asciiend
    rotateButtonText.y = confirmShips.y + 17
    rotateButton.w = 5
    rotateButton.h = 4

// ANCHOR Preview and place ships (player)
func AddShips (tile)
  // Allow updates
  allowUpdates = true

  // Add a preview board of pTiles
  var previewTiles = BoardCopy(pTiles)

  // Tile position
  var y = (tile.y - 1) / (playerGridPanel.h / 10)
  var x = (tile.x - 1) / (playerGridPanel.w / 10)

  // Add ship preview
  ?previewTiles[y][x] = 1 & placedShipCoordsFlattened.Count() + selectedShipSize <= shipTileCount
    // Detected existing ship tiles
    var previewShipYX = []

    // Clearing previously stored previewed ships
    for coords : previewedShipCoords
      previewTiles[coords[0]][coords[1]] = 1
    previewedShipCoords.Clear()

    // Ship direction
    var dir
    ?placeHorizontal
      dir = directions[1]
    :
      dir = directions[0]
    var dY = dir[0]
    var dX = dir[1]

    // If ship size is not selected, throw an error
    ?selectedShipSize = 0
      return PrintSelectionError()

    // Tiles
    var firstTileY = y
    var firstTileX = x
    var tile2Y = firstTileY + (1 * dY)
    var tile2X = firstTileX + (1 * dX)
    var tile3Y = firstTileY + (2 * dY)
    var tile3X = firstTileX + (2 * dX)
    var tile4Y = firstTileY + (3 * dY)
    var tile4X = firstTileX + (3 * dX)
    var tileListY = [firstTileY, tile2Y, tile3Y, tile4Y]
    var tileListX = [firstTileX, tile2X, tile3X, tile4X]
    var originalFirstTileY = firstTileY
    var originalFirstTileX = firstTileX
    for yx = 1..50
      ?selectedShipSize = 1
        previewTiles[firstTileY][firstTileX] = 12
        previewedShipCoords.Add([firstTileY, firstTileX])
        break
      for t = 0..selectedShipSize-1
        var tileIDs = []
        var badIDY
        var badIDX
        badIDY = false
        badIDX = false
        for i = 0..selectedShipSize-1
          ?tileListY[i] >= 0 & tileListY[i] <= 9 & tileListX[i] >= 0 & tileListX[i] <= 9
            tileIDs.Add(previewTiles[tileListY[i]][tileListX[i]])
          :
            break
          ?![1, 12].Contains(tileIDs[i])
            ?tileListY[0] = tileListY[1]
              badIDX = true
            :
              badIDY = true
        tileIDs.Clear()
        ?tileListY[selectedShipSize-1] > 9 | tileListX[selectedShipSize-1] > 9 | badIDY | badIDX
          previewShipYX.Clear()
          ?(tileListY[selectedShipSize-1] > 9 | badIDY) & tileListY[selectedShipSize-1] >= originalFirstTileY & firstTileY > 0
            firstTileY --
          :?(tileListX[selectedShipSize-1] > 9 | badIDX) & tileListX[selectedShipSize-1] >= originalFirstTileX & firstTileX > 0
            firstTileX --
          :
            Flash(pButtonText[y][x], "text", "#ff0000", pButtonText[y][x].color, 20)
            dontPrintPreviewInstructions = true
            break
          tile2Y = firstTileY + (1 * dY)
          tile2X = firstTileX + (1 * dX)
          tile3Y = firstTileY + (2 * dY)
          tile3X = firstTileX + (2 * dX)
          tile4Y = firstTileY + (3 * dY)
          tile4X = firstTileX + (3 * dX)
          tileListY = [firstTileY, tile2Y, tile3Y, tile4Y]
          tileListX = [firstTileX, tile2X, tile3X, tile4X]
        :
          for p = 0..selectedShipSize-1
            previewShipYX.Add([tileListY[p], tileListX[p]])
          break
      ?previewShipYX ! [] & previewShipYX.Count() = selectedShipSize
        for i = 0..previewShipYX.Count()-1
          previewTiles[previewShipYX[i][0]][previewShipYX[i][1]] = 12
          previewedShipCoords.Add([previewShipYX[i][0], previewShipYX[i][1]])
        break
    ?dontPrintPreviewInstructions
      dontPrintPreviewInstructions = false
      printPreviewInstructions = false
    :
      printPreviewInstructions = true

  // Confirm previewed ship
  :?previewTiles[y][x] = 12
    printPreviewInstructions = false
    
    placedShipCoords.Add([])
    for coords : previewedShipCoords
      previewTiles[coords[0]][coords[1]] = 2
      placedShipCoords[placedShipCoords.Count()-1].Add([coords[0], coords[1]])
    placedShips[previewedShipCoords.Count()-1] += 1
    previewedShipCoords.Clear()
        
  // Remove a ship
  :?previewTiles[y][x] = 2
    for i = 0..placedShipCoords.Count()-1
      var isTrue = false
      for j = 0..placedShipCoords[i].Count()-1
        ?placedShipCoords[i][j][0] = y & placedShipCoords[i][j][1] = x
          isTrue = true
        ?isTrue
          for k = 0..placedShipCoords[i].Count()-1
            previewTiles[placedShipCoords[i][k][0]][placedShipCoords[i][k][1]] = 1
          placedShips[placedShipCoords[i].Count()-1] -= 1
          break
      ?isTrue
        placedShipCoords.RemoveAt(i)
        break
  
  // Wrong placement animation
  :
    Flash(pButtonText[y][x], "text", "#ff0000", pButtonText[y][x].color, 20)

  // Update restricted tiles
  for y = 0..9
    for x = 0..9
      ?previewTiles[y][x] = 0
        previewTiles[y][x] = 1
  for y = 0..9
    for x = 0..9
      ?previewTiles[y][x] = 2
        var sx = [-1, 0, 1]
        var sy = [-1, 0, 1]
        for sxi : sx
          for syi : sy
            ?y + syi < 0 | x + sxi < 0 | y + syi > 9 | x + sxi > 9
              continue
            ?!(sxi = 0 & syi = 0)
              ?previewTiles[y + syi][x + sxi] = 1
                previewTiles[y + syi][x + sxi] = 0
  
  // Copy and flatten placed ship tiles in a new array
  placedShipCoordsFlattened.Clear()
  ?placedShipCoords.Count()>0
    for i = 0..placedShipCoords.Count()-1
      ?placedShipCoords[i].Count()>0
        for j = 0..placedShipCoords[i].Count()-1
          placedShipCoordsFlattened.Add(placedShipCoords[i][j])
  
  // Assign preview tiles to pTiles
  for y = 0..9
    for x = 0..9
      pTiles[y][x] = previewTiles[y][x]

// TODO Multiplayer mode
// .ANCHOR Add ships manually (player)
/* func AddShip_s (tile)
  // Add ship tiles one by one
  var y = (tile.y - 1) / (playerGridPanel.h / 10)
  var x = (tile.x - 1) / (playerGridPanel.w / 10)
  ?pTiles[y][x] = 2
    for i : placedShipCoords
      ?i[0] = y & i[1] = x
        placedShipCoords.RemoveAt(placedShipCoords.IndexOf(i))
        break
    pTiles[y][x] = 1
    pButtonText[y][x].text = "1"
    pButtonText[y][x].color = "#00aaff"
  :?placedShipCoords.Count() < shipTileCount & pTiles[y][x] = 1
    placedShipCoords.Add([y, x])
    pTiles[y][x] = 2
    pButtonText[y][x].text = "2"
    pButtonText[y][x].color = "#ffff77" */

// ANCHOR Add markers, hit ships (player)
func AddMarker (tile)
  // Allow updates
  allowUpdates = true

  // Separate P and E variables
  var GridPanel
  var Tiles
  var ButtonText
  var Markers
  ?tile.parent = playerGridPanel
    GridPanel = playerGridPanel
    Tiles = pTiles
    Markers = playerMarkers
  :?tile.parent = enemyGridPanel
    GridPanel = enemyGridPanel
    Tiles = eTiles
    Markers = enemyMarkers
  
  // Add ship tiles one by one
  var y = (tile.y - 1) / (GridPanel.h / 10)
  var x = (tile.x - 1) / (GridPanel.w / 10)
  ?markerMode = true
    ?Tiles[y][x] = 0 | Tiles[y][x] = 1 | (Tiles = eTiles & Tiles[y][x] = 2)
      Markers.Add([y, x, Tiles[y][x]])  
      Tiles[y][x] = 6
    :?Tiles[y][x] = 6
      for i : Markers
        ?i[0] = y & i[1] = x
          Tiles[y][x] = i[2]
          Markers.RemoveAt(Markers.IndexOf(i))
          break
  :?Tiles = eTiles & playerTurn = true
    ?Tiles[y][x] = 2
      Tiles[y][x] = 4
      KillShip(Tiles, y, x)
      pHitScore ++
      // (Continue turn if shot hits)
    :?Tiles[y][x] = 0 | Tiles[y][x] = 1
      Tiles[y][x] = 3
      playerTurn = false
    playerLastAttack = [y, x]

// ANCHOR Kill ships
func KillShip (Tile, y, x)
  var hitTiles = [[y, x]]
  var notDead = false
  var SinkScore = 0
  for i = 0..3
    y = hitTiles[i][0]
    x = hitTiles[i][1]
    var sx = [-1, 0, 1]
    var sy = [-1, 0, 1]
    for sxi : sx
      for syi : sy
        ?sxi = 0 & syi = 0
          continue
        var dy = y + syi
        var dx = x + sxi
        ?dy < 0 | dx < 0 | dy > 9 | dx > 9
          continue
        ?Tile[dy][dx] = 2
          notDead = true
          break
        var hitTilesContains = false
        for i = 0..hitTiles.Count()-1
          ?hitTiles[i][0] = dy & hitTiles[i][1] = dx
            hitTilesContains = true
            break
        ?Tile[dy][dx] = 4 & hitTilesContains = false
          hitTiles.Add([dy, dx])
      ?notDead
        break
    ?i >= hitTiles.Count()-1 | notDead
      break

  ?!notDead
    var removeFromList = []
    var isPTILES
    ?Tile = pTiles
      isPTILES = true
    for yx : hitTiles
      Tile[yx[0]][yx[1]] = 5
      SinkScore ++
      ?eHitList.Count() > 0 & isPTILES
        for i = 0..eHitList.Count()-1
          ?eHitList[i][0] = yx[0] & eHitList[i][1] = yx[1]
            removeFromList.Add(i)
    ?eHitList.Count() > 0 & isPTILES
      removeFromList.Sort()
      for i = 0..removeFromList.Count()-1
        eHitList.RemoveAt(removeFromList[removeFromList.Count() - i - 1])
  
  // Add score
  ?Tile = eTiles
    pSinkScore += SinkScore
  :
    eSinkScore += SinkScore

// ANCHOR Reset a board to empty
func BoardReset (board)
  board.Clear()
  for y = 0..9
    var row = []
    for x = 0..9
      row.Add(1)
    board.Add(row)

// ANCHOR Copy a board
func BoardCopy (board)
  var column = []
  for y = 0..9
    var row = []
    for x = 0..9
      row.Add(board[y][x])
    column.Add(row)
  return column

// ANCHOR Copy a matrix/array of arrays
func MatrixCopy (arr)
  var copy = []
  for i : arr
    ?Type(i) = "array"
      copy.Add(MatrixCopy(i))
    :
      copy.Add(i)
  return copy

// ANCHOR PlaceAllShipsError
// Displays the error message "Place all ships" by resetting its timer
func ResetErrorTimer ()
  boolSwitchTimer = 0

// ANCHOR SelectAShipError
// Displays the error message "Select a ship" by resetting its timer
func PrintSelectionError ()
  selectionErrTimer = 0

// ANCHOR Start gameplay
func StartGameplay ()
  // Allow updates
  allowUpdates = true
  
  // Reveal enemy board
  enemyGridPanel.visible = true
  markerButton.visible = true
  confirmShips.visible = false
  confirmShipsText.visible = false
  ship4.visible = false
  ship3.visible = false
  ship2.visible = false
  ship1.visible = false
  ship4text.visible = false
  ship3text.visible = false
  ship2text.visible = false
  ship1text.visible = false
  rotateButton.visible = false

  // Reset selectedShipSize
  selectedShipSize = 4

  // Pinpoint player ship weapons
  for ships : placedShipCoords
    ?ships.Count() > 1
      var yAbsPos
      var xAbsPos
      var wepType
      // Horizontal ships
      ?ships[0][0] = ships[1][0]
        ?ships.Count() = 3
          yAbsPos = pButtons[ships[2][0]][ships[2][1]].absoluteY
          xAbsPos = pButtons[ships[2][0]][ships[2][1]].absoluteX + 1
          wepType = "ballista"
        :?ships.Count() = 4
          yAbsPos = pButtons[ships[2][0]][ships[2][1]].absoluteY - 1
          xAbsPos = pButtons[ships[2][0]][ships[2][1]].absoluteX + 4
          wepType = "catapult"
        :
          continue
        playerWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])
      // Vertical ships
      :
        ?[2, 3].Contains(ships.Count()) // ships.Count() = 2
          yAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteY
          xAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteX + 4
          wepType = "cannon"
        // :?ships.Count() = 3
        //   yAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteY
        //   xAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteX + 4
        //   wepType = "cannon"
        :?ships.Count() = 4
          yAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteY + 1
          xAbsPos = pButtons[ships[1][0]][ships[1][1]].absoluteX + 4
          wepType = "cannon"
          playerWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])
          yAbsPos = pButtons[ships[2][0]][ships[2][1]].absoluteY
        :
          continue
        playerWeaponAbsPos.Add([yAbsPos, xAbsPos, wepType])
  
  // Show buttons on enemy board,
  // enable markers on player board
  for y = 0..9
    for x = 0..9
      eButtons[y][x].visible = "inherit"
      pButtons[y][x].SetPressed(AddMarker)

// ANCHOR Generate a random number in a given range
func RandIntInRange (min, max)
  // return math.FloorToInt(((rng * 0.61803398875) % 1) * (max + 1)) - min
  return (rng % (max - min + 1)) + min

// ANCHOR Toggle marker mode on/off
func ToggleMarkerMode ()
  ?enemyGridPanel.visible
    markerMode = !markerMode

// ANCHOR Flash
func Flash (component, type, flashColor, prevColor, duration)
  // flashListCopy components:
  // [component, "type", "#flash color", "#original color", current duration, max duration]

  ?flashColor = prevColor
    ?flashColor = "#ff0000"
      allowUpdates = true
    return
  
  var frequency = 3
  ?flashList.Count()
    for i = 0..flashList.Count()-1
      ?flashList[i][0] = component
        // Flash
        var elapsedTime
        elapsedTime = flashList[i][5] - flashList[i][4]
        ?elapsedTime % (frequency * 2) < frequency
          ?type = "button"
            component.bcolor = flashColor
            component.tcolor = flashColor
          :
            component.color = flashColor
        :
          ?type = "button"
            component.bcolor = prevColor
            component.tcolor = prevColor
          :
            component.color = prevColor
        flashList[i][4] -= 1
        ?flashList[i][4] <= 0
          ?type = "button"
            component.bcolor = prevColor
            component.tcolor = prevColor
          :
            component.color = prevColor
          flashList.RemoveAt(i)
        return
  
  flashList.Add([component, type, flashColor, prevColor, duration, duration])

// ANCHOR Add a new UI element
func AddUI (Type, Anchor, Dock, wd, hi, xPos, yPos)
  var comp
  ?Type = "button"
    comp = ui.AddButton()
  :?Type = "text"
    comp = ui.AddText()
  :?Type = "panel"
    comp = ui.AddPanel()
  :?Type = "anim"
    comp = ui.AddAnim()
  :?Type = "canvas"
    comp = ui.AddCanvas()
  comp.anchor = Anchor
  comp.dock = Dock
  comp.w = wd
  comp.h = hi
  comp.x = xPos
  comp.y = yPos
  return comp

// ANCHOR Toggle panel visibility on button press
func TogglePanel (btn)
  var panel
  ?btn = infoButton
    panel = infoPanel
  panel.visible = !panel.visible

// ANCHOR Does nothing. For buttons
func Nothing ()
  return

// ===================================
// !SECTION
// ===================================


// ===================================
// SECTION Board updates & art
// ===================================

// ANCHOR Textures
var waterTexture = "#. #\n# °#"
var flagTexture = "#│▶#\n⌐¯¬#"
// flagTexture = "#│>#\n#¯-#"
// flagTexture = "#│▶#\n#¯-#"
// flagTexture = "#│>#\n-¯-#"
// flagTexture = "#│▶#\n-¯-#"
// flagTexture = "#│>#\n⌐¯¬#"
// flagTexture = "#│▶#\n⌐¯¬#"
// flagTexture = "#│>#\n┌┴┐#"
// flagTexture = "#│▶#\n┌┴┐#"
// flagTexture = "#│>#\n─┴─#"
// flagTexture = "#│▶#\n─┴─#"
// flagTexture = "#│>#\n´¯`#"
// flagTexture = "#│▶#\n´¯`#"
var hitTileTexture = "#x #\n# x#"
var hitShipHiddenTexture = "#\\/#\n#/\#"  // draws "X" on hit ships

// ANCHOR Update text on tile buttons
?allowUpdates
  for y = 0..9
    for x = 0..9      
      // Player tile color
      ?!enemyGridPanel.visible & pTiles[y][x] = 0
        pButtonText[y][x].color = "#555577"
      :?pTiles[y][x] = 0 | pTiles[y][x] = 1
        pButtonText[y][x].color = "#3ba2d5" //"#00aaff"
      :?pTiles[y][x] = 2
        pButtonText[y][x].color = "#c89e29" //"#ffcc44"
      :?pTiles[y][x] = 3
        pButtonText[y][x].color = "#01354f" //"#0000e0"
      :?pTiles[y][x] = 4
        pButtonText[y][x].color = "#ca2b2b" //"#ff0101"
      :?pTiles[y][x] = 5
        pButtonText[y][x].color = "#4c392b"
      :?pTiles[y][x] = 6
        pButtonText[y][x].color = "#17ac5a"
      :?pTiles[y][x] = 12
        pButtonText[y][x].color = "#c523c5" //"#ff00ff"
      :?pTiles[y][x] >= 10
        pButtonText[y][x].color = "#690069"
      // Enemy tile color
      ?eTiles[y][x] = 0
        eButtonText[y][x].color = "#3ba2d5" //"#00aaff" //"#555577"
      :?eTiles[y][x] = 1
        eButtonText[y][x].color = "#3ba2d5" //"#00aaff"
      :?eTiles[y][x] = 2
        eButtonText[y][x].color = "#3ba2d5" //"#00aaff" //"#ffcc44"
      :?eTiles[y][x] = 3
        eButtonText[y][x].color = "#01354f" //"#0000e0"
      :?eTiles[y][x] = 4
        eButtonText[y][x].color = "#ca2b2b" //"#ff0101"
      :?eTiles[y][x] = 5
        eButtonText[y][x].color = "#4c392b"
      :?eTiles[y][x] = 6
        eButtonText[y][x].color = "#17ac5a"

      // Change tile ID to ASCII
        // Player
      ?pTiles[y][x] = 0 | pTiles[y][x] = 1
        pButtonText[y][x].text = waterTexture
      :?pTiles[y][x] = 2 | pTiles[y][x] = 4 | pTiles[y][x] = 5
        for L1 : placedShipCoords
          ?L1.Count() = 1 & L1[0][0] = y & L1[0][1] = x
            pButtonText[y][x].text = ship1text.text
          :?L1.Count() > 1
            for i = 0..L1.Count()-1
              ?L1[0][0] = L1[1][0] & L1[0][0] = y
                ?L1[i][1] = x
                  ?L1.Count() = 2
                    pButtonText[y][x].text = ship2horizontal[i]
                  :?L1.Count() = 3
                    pButtonText[y][x].text = ship3horizontal[i]
                  :?L1.Count() = 4
                    pButtonText[y][x].text = ship4horizontal[i]
              :?L1[0][1] = L1[1][1] & L1[0][1] = x
                ?L1[i][0] = y
                  ?L1.Count() = 2
                    pButtonText[y][x].text = ship2vertical[i]
                  :?L1.Count() = 3
                    pButtonText[y][x].text = ship3vertical[i]
                  :?L1.Count() = 4
                    pButtonText[y][x].text = ship4vertical[i]
      :?pTiles[y][x] = 12
        ?previewedShipCoords.Count() = 1 & previewedShipCoords[0][0] = y & previewedShipCoords[0][1] = x
          pButtonText[y][x].text = ship1text.text
        :?previewedShipCoords.Count() > 1
          for i = 0..previewedShipCoords.Count()-1
            ?previewedShipCoords[0][0] = previewedShipCoords[1][0] & previewedShipCoords[0][0] = y
              ?previewedShipCoords[i][1] = x
                ?previewedShipCoords.Count() = 2
                  pButtonText[y][x].text = ship2horizontal[i]
                :?previewedShipCoords.Count() = 3
                  pButtonText[y][x].text = ship3horizontal[i]
                :?previewedShipCoords.Count() = 4
                  pButtonText[y][x].text = ship4horizontal[i]
            :?previewedShipCoords[0][1] = previewedShipCoords[1][1] & previewedShipCoords[0][1] = x
              ?previewedShipCoords[i][0] = y
                ?previewedShipCoords.Count() = 2
                  pButtonText[y][x].text = ship2vertical[i]
                :?previewedShipCoords.Count() = 3
                  pButtonText[y][x].text = ship3vertical[i]
                :?previewedShipCoords.Count() = 4
                  pButtonText[y][x].text = ship4vertical[i]
      :?pTiles[y][x] = 3
        pButtonText[y][x].text = hitTileTexture
      :?pTiles[y][x] = 6
        pButtonText[y][x].text = flagTexture
        
        // Enemy
      ?enemyGridPanel.visible & eButtons ! []
        ?eTiles[y][x] = 0 | eTiles[y][x] = 1 | eTiles[y][x] = 2
          eButtonText[y][x].text = waterTexture
        :?eTiles[y][x] = 4
          eButtonText[y][x].text = hitShipHiddenTexture
        :?eTiles[y][x] = 5
          for L1 : placedEnemyShipCoords
            ?L1.Count() = 1 & L1[0][0] = y & L1[0][1] = x
              eButtonText[y][x].text = ship1textEnemy
            :?L1.Count() > 1
              for i = 0..L1.Count()-1
                ?L1[0][0] = L1[1][0] & L1[0][0] = y
                  ?L1[i][1] = x
                    ?L1.Count() = 2
                      eButtonText[y][x].text = ship2horizontalEnemy[i]
                    :?L1.Count() = 3
                      eButtonText[y][x].text = ship3horizontalEnemy[i]
                    :?L1.Count() = 4
                      eButtonText[y][x].text = ship4horizontalEnemy[i]
                :?L1[0][1] = L1[1][1] & L1[0][1] = x
                  ?L1[i][0] = y
                    ?L1.Count() = 2
                      eButtonText[y][x].text = ship2verticalEnemy[i]
                    :?L1.Count() = 3
                      eButtonText[y][x].text = ship3verticalEnemy[i]
                    :?L1.Count() = 4
                      eButtonText[y][x].text = ship4verticalEnemy[i]
        /* :?eTiles[y][x] = 12
          ?previewedEnemyShipCoords.Count() = 1 & previewedEnemyShipCoords[0][0] = y & previewedEnemyShipCoords[0][1] = x
            eButtonText[y][x].text = ship1textEnemy
          :?previewedEnemyShipCoords.Count() > 1
            for i = 0..previewedEnemyShipCoords.Count()-1
              ?previewedEnemyShipCoords[0][0] = previewedEnemyShipCoords[1][0] & previewedEnemyShipCoords[0][0] = y
                ?previewedEnemyShipCoords[i][1] = x
                  ?previewedEnemyShipCoords.Count() = 2
                    eButtonText[y][x].text = ship2horizontalEnemy[i]
                  :?previewedEnemyShipCoords.Count() = 3
                    eButtonText[y][x].text = ship3horizontalEnemy[i]
                  :?previewedEnemyShipCoords.Count() = 4
                    eButtonText[y][x].text = ship4horizontalEnemy[i]
              :?previewedEnemyShipCoords[0][1] = previewedEnemyShipCoords[1][1] & previewedEnemyShipCoords[0][1] = x
                ?previewedEnemyShipCoords[i][0] = y
                  ?previewedEnemyShipCoords.Count() = 2
                    eButtonText[y][x].text = ship2verticalEnemy[i]
                  :?previewedEnemyShipCoords.Count() = 3
                    eButtonText[y][x].text = ship3verticalEnemy[i]
                  :?previewedEnemyShipCoords.Count() = 4
                    eButtonText[y][x].text = ship4verticalEnemy[i] */
        :?eTiles[y][x] = 3
          eButtonText[y][x].text = hitTileTexture
        :?eTiles[y][x] = 6
          eButtonText[y][x].text = flagTexture

// ANCHOR Flash (red)
?flashListCopy.Count() | flashList.Count()
  for i : flashListCopy
    Flash(i[0], i[1], i[2], i[3], i[4])

// ANCHOR Disable updates
?playerTurn | turnDelay > 1 | !playerGridPanel.visible
  allowUpdates = false

// ===================================
// !SECTION
// ===================================


disable hud b
disable pause

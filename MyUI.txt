/* 
----- MyUI v1.2 -----
  --- by Davit  ---

A group of UI elements.
Includes:
- time
- buffs and debuffs
- item cooldowns
- better player/foe hp (bottom) line
- foe radar

Changelog:
v1.1 - more accurate buff/debuff time display.
v1.2 - draggable ui, added loc name and stars to the time panel
*/



// ------------------------------------------- Misc ------------------------------------------- //

// ----- Draggable panels ----- //
var drag = import UI/DragController
drag.Update()

// ----- Toggle a panel on/off ----- //
func TogglePanel (btn)
  var panel
  ?btn = radarToggleBtn
    panel = radarPanel
    // ?buffsPanel.y = cdPanel.y
    //   buffsPanel.x = cdPanel.x
    //   buffsPanel.y = cdPanel.y + cdPanel.h
    // :
    //   buffsPanel.x = cdPanel.x - cdPanel.w
    //   buffsPanel.y = cdPanel.y
  :?btn = cdToggleBtn
    panel = cdPanel
  :?btn = buffsToggleButton
    panel = buffsPanel
  :?btn = timeToggleButton
    panel = timePanel

  panel.visible = !panel.visible

  ?panel.visible = true
    btn.text = "↓"
  :
    btn.text = "↑"


// ----- UI element quick setup ----- //
func SetUI (elem, align, w, h, x, y)
  elem.anchor = align
  elem.dock = align
  elem.w = w
  elem.h = h
  elem.x = x
  elem.y = y

// ----- Rune cycle ----- //
var rune
var element
var elements =
^ ["poison", "vigor", "aether", "fire", "ice"]

?foe = poison vigor aether fire ice
  rune = elements[0]
  element = "§"
:?foe = poison
  rune = elements[4]
  element = "∞"
:?foe = vigor
  rune = elements[0]
  element = "♥"
:?foe = aether
  rune = elements[1]
  element = "*"
:?foe = fire
  rune = elements[2]
  element = "φ"
:?foe = ice
  rune = elements[3]
  element = "❄"
:?(foe ! phase & loc = rocky) | loc = deadwood
^| (loc = temple & foe ! poison)
  rune = "stone"
  element = "ʘ"
:
  rune = ""
  element = ""



// ----------------------------------------- Cooldown ----------------------------------------- //

// ----- Hidden items ----- //
// Wands
var wands = ["wand_stone", "wand_poison",
^"wand_vigor", "wand_aether", "wand_fire",
^"wand_ice"]

// Staves
var staves = ["quarterstaff", "staff_stone",
^"staff_poison", "staff_vigor",
^"staff_aether", "staff_fire", "staff_ice"]


// ----- Colorful cooldown ----- //
func cooldown (id)
  var cdcolor
  var cd
  cd = item.GetCooldown(id)
  ?cd = 0
    cdcolor = "#00e663"
  :?cd < 0
    cdcolor = "#00caff"
  :
    cdcolor = "#e30039"
  return [cdcolor, cd]


// ----- Cooldown printer ----- //
func printcd (id)
  var cd = cooldown(id)
  var txt
  ?id = staff
    ?id = quarterstaff
      txt = "/:"
    :?id = stone
      txt = "ʘ:"
    :?id = poison
      txt = "∞:"
    :?id = vigor
      txt = "♥:"
    :?id = aether
      txt = "*:"
    :?id = fire
      txt = "φ:"
    :?id = ice
      txt = "❄:"
  :?id = wand
    txt = ""
  :?id = "blade"
    txt = "Blade:"
  :?id = "mask"
    txt = "Mask :"
  :?id = "skeleton_arm"
    txt = "Arm  :"
  :?id = "bardiche"
    txt = "Bard :"
  :?id = "heavy_hammer"
    txt = "Hamm :"
  :?id = "dash"
    txt = "Dash :"
  :?id = "bash"
    txt = "Bash :"
  :?id = "mind"
    txt = "Mind :"
  :
    var len
    var spacecount = 0
    var space = ""
    var lenid = string.Size(id)
    ?lenid >= 5
      len = 5
    :
      len = lenid
      spacecount = 5 - lenid
    for i = 1..spacecount
      space += " "
    txt = string.Sub(string.Capitalize(id),
    ^0, 5) + space + ": "
  var res1 = "[color=#e0e0ff]"+txt+"[/color]"
  var res2 = "[color="+cd[0]+"]"+cd[1]/30+"s[/color]"
  var result = res1 + res2
  return result


// ----- Display item cooldown ----- //
// Set up UI elements
var cdList1
cdList1 = [
^printcd("blade"),
^printcd("mask"),
^printcd("skeleton_arm"),
^printcd("bardiche"),
^printcd("heavy_hammer"),
^printcd("dash"),
^printcd("bash"),
^printcd("mind")]

var cdList2 = []
cdList2.Add("  [color=#aeaec6]s[/color]")
for i = 0..staves.Count()-1
  cdList2.Add(printcd(staves[i]))
  
var cdList3 = []
cdList3.Add("[color=#aeaec6]w[/color]\n-")
for i = 0..wands.Count()-1
  cdList3.Add(printcd(wands[i]))

var cdPanel
var cdToggleBtn
var cdText0
var cdText1
var cdText2
var cdText3

?loc.begin | loc.loop
  cdPanel = ui.AddPanel()
  SetUI(cdPanel, top_left, 24, 10, 20, 1)
  cdPanel.color = "#5B57C8"
  drag.Add(cdPanel)
  
  cdText0 = ui.AddText()
  SetUI(cdText0, top_left, 9, 1, 1, 0)
  cdPanel.Add(cdText0)
  cdText0.text = "Cooldown"
  cdText0.color = "#5B57C8"
  
  cdText1 = ui.AddText()
  SetUI(cdText1, top_left, 11, 9, 2, 1)
  cdText1.color = "#e0e0ff"
  cdPanel.Add(cdText1)
  
  cdText2 = ui.AddText()
  SetUI(cdText2, top_left, 6, 9, 13, 1)
  cdText2.color = "#e0e0ff"
  cdPanel.Add(cdText2)
  
  cdText3 = ui.AddText()
  SetUI(cdText3, top_left, 3, 9, 19, 1)
  cdText3.color = "#e0e0ff"
  cdPanel.Add(cdText3)

  cdToggleBtn = ui.AddButton()
  SetUI(cdToggleBtn, bottom_right, 5, 3, -9, -1)
  cdToggleBtn.text = "↑"
  cdToggleBtn.bcolor = "#5B57C8"
  cdToggleBtn.tcolor = "#5B57C8"
  cdToggleBtn.SetPressed(TogglePanel)

?loc.begin
  cdPanel.visible = false
var cdPanel_isVisible = cdPanel.visible
?loc.loop
  cdPanel.visible = cdPanel_isVisible
:
  cdPanel_isVisible = cdPanel.visible

// Add the text
var cdT1 = ""
for i : cdList1
  cdT1 += i + "\n"
cdText1.text = cdT1

var cdT2 = ""
for i : cdList2
  cdT2 += i + "\n"
cdText2.text = cdT2

var cdT3 = ""
for i : cdList3
  cdT3 += i + "\n"
cdText3.text = cdT3

// Reset variables
cdList1 = []
cdList2 = []
cdList3 = []
cdT1 = ""
cdT2 = ""
cdT3 = ""



// ------------------------------------ Buffs and debuffs ------------------------------------ //

// ----- Arrange the (de)buffs string ----- //
func formatBuffs (str)
  ?str
    var result = ""
    for buff : string.Split(str, ",")
      var barr = string.Split(buff, ":")
      ?result
        result += ";"
      ?barr[1] = "debuff"
        barr[1] = string.Sub(barr[1], 6)
      :?barr[1] = "buff"
        barr[1] = string.Sub(barr[1], 4)
      ?string.Size(barr[1]) >= 8
        barr[1] = string.Sub(barr[1], 0, 6)
      string.Capitalize(barr[1])
      result += barr[2]+barr[0]+" "+barr[1]
      result += ": "+(int.Parse(barr[3])/30+1)+"s"
    return result

var fbstr  // Character limit bypass
var fdbstr
var bstr
var dbstr
fbstr = formatBuffs(foe.buffs.string)
fdbstr = formatBuffs(foe.debuffs.string)
bstr = formatBuffs(buffs.string)
dbstr = formatBuffs(debuffs.string)
var fbl
var fdbl
var bl
var dbl
?fbstr
  fbl = string.Split(fbstr,";")
:
  fbl = []
?fdbstr
  fdbl = string.Split(fdbstr,";")
:
  fdbl = []
?bstr
  bl = string.Split(bstr,";")
:
  bl = []
?dbstr
  dbl = string.Split(dbstr,";")
:
  dbl = []


// ----- Store (de)buffs ----- //
var f_d_b_l = []  // universal b db fb fdb var

// Player (de)buffs
?bl.Count() > 0
  f_d_b_l.Add("Buffs:")
  for i = 0..bl.Count()-1
    f_d_b_l.Add("[color=#00e663]" + bl[i] + "[/color]")
?dbl.Count() > 0
  f_d_b_l.Add("Debuffs:")
  for i = 0..dbl.Count()-1
    f_d_b_l.Add("[color=#a70c26]" + dbl[i] + "[/color]")

// Enemy (de)buffs
?fbl.Count() > 0
  f_d_b_l.Add("Foe buffs:")
  for i = 0..fbl.Count()-1
    f_d_b_l.Add("[color=#a70c26]" + fbl[i] + "[/color]")
?fdbl.Count() > 0
  f_d_b_l.Add("Foe debuffs:")
  for i = 0..fdbl.Count()-1
    f_d_b_l.Add("[color=#00e663]" + fdbl[i] + "[/color]")


// ----- UI elements ----- //
var buffsPanel
var buffsPanelTitle
var buffsToggleButton
var buffsText

?loc.begin | loc.loop
  buffsPanel = ui.AddPanel()
  SetUI(buffsPanel, top_right, 20, 2, -2, 1)
  buffsPanel.color = "#F01D96"
  buffsPanel.visible = false
  drag.Add(buffsPanel)

  buffsPanelTitle = ui.AddText()
  SetUI(buffsPanelTitle, top_left, 5, 1, 1, 0)
  buffsPanel.Add(buffsPanelTitle)
  buffsPanelTitle.text = "Buffs"
  buffsPanelTitle.color = "#F01D96"

  buffsText = ui.AddText()
  SetUI(buffsText, top_left, 18, buffsText.w, 1, 1)
  buffsPanel.Add(buffsText)

  buffsToggleButton = ui.AddButton()
  SetUI(buffsToggleButton, bottom_right, 5, 3, -14, -1)
  ?buffsPanel.visible
  buffsToggleButton.text = "↑"
  buffsToggleButton.bcolor = "#F01D96"
  buffsToggleButton.tcolor = "#F01D96"
  buffsToggleButton.SetPressed(TogglePanel)

?loc.begin
  buffsPanel.visible = false
var buffsPanel_isVisible = buffsPanel.visible
?loc.loop
  buffsPanel.visible = buffsPanel_isVisible
:
  buffsPanel_isVisible = buffsPanel.visible

var buffsTextGhost = ""
for i : f_d_b_l
  buffsTextGhost += i + "\n"
buffsText.text = buffsTextGhost
buffsPanel.h = f_d_b_l.Count() + 2

// ----- Reset variables ----- //
buffsTextGhost = ""
f_d_b_l = []



// ---------------------------------------- Foe radar ---------------------------------------- //

// ----- Find enemy position ----- //
func SearchEnemy (range)
  var foeCount = foe.GetCount(range)
  var locatedFoeN
  var locatedFoePos = []
  for i = 1..range
    locatedFoeN = locatedFoePos.Count()
    ?locatedFoeN = foeCount
      break
    ?foe.GetCount(i) > locatedFoeN
      for j = 1..foe.GetCount(i)-locatedFoeN
        locatedFoePos.Add(i)
  ?locatedFoePos.Count() > 0
    for i = 0..locatedFoePos.Count()-1
      locatedFoePos[i] = math.FloorToInt(locatedFoePos[i] * 49 / range)
  var output = [locatedFoeN, locatedFoePos]
  return output

var radarRange
radarRange = 50  // Works perfectly with ranges [5, 10, 25, 50, 125, 250]. Works ok with [63, 83]
var searchResults
searchResults = SearchEnemy(radarRange)
var locatedFoeN
locatedFoeN = searchResults[0]
var locatedFoePos
locatedFoePos = searchResults[1]


// ----- UI elements ----- //

// Radar
var radarPanel
var radarToggleBtn
var radarText0
var radarText1
var radarMap2
var radarMap
var radarMapRuler

?loc.begin | loc.loop
  radarPanel = ui.AddPanel()
  SetUI(radarPanel, top_left, 54, 6, 0, 16)
  radarPanel.color = "#00cc44"
  radarPanel.visible = false
  drag.Add(radarPanel)
  
  radarText0 = ui.AddText()
  SetUI(radarText0, top_left, 6, 1, 1, 0)
  radarPanel.Add(radarText0)
  radarText0.text = "Radar"
  radarText0.color = "#00cc44"
  
  radarText1 = ui.AddText()
  SetUI(radarText1, top_left, 17, 1, 2, 1)
  radarPanel.Add(radarText1)
  
  radarMap2 = ui.AddText()
  SetUI(radarMap2, top_left, 50, 1, 2, 2)
  radarPanel.Add(radarMap2)
  var rMt2 = "                         "
  radarMap2.text = rMt2 + rMt2
  radarMap2.color = #red
  
  radarMap = ui.AddText()
  SetUI(radarMap, top_left, 50, 1, 2, 3)
  radarPanel.Add(radarMap)
  radarMap.text = ".................................................."
  radarMap.color = "#00cc44"
  
  radarMapRuler = ui.AddText()
  SetUI(radarMapRuler, top_left, 50, 1, 2, 4)
  radarPanel.Add(radarMapRuler)
  
  radarToggleBtn = ui.AddButton()
  SetUI(radarToggleBtn, bottom_right, 5, 3, -19, -1)
  radarToggleBtn.text = "↑"
  radarToggleBtn.bcolor = "#00cc44"
  radarToggleBtn.tcolor = "#00cc44"
  radarToggleBtn.SetPressed(TogglePanel)

?loc.begin
  radarPanel.visible = false
var radarPanel_isVisible = radarPanel.visible
?loc.loop
  radarPanel.visible = radarPanel_isVisible
:
  radarPanel_isVisible = radarPanel.visible

radarText1.text = "Foes in sight: [color=#red]"+locatedFoeN+"[/color]"

var radarPts2 = []
for i = 0..49
  radarPts2.Add(" ")
var radarPts
radarPts = string.Break("..................................................", 1)
?locatedFoePos.Count() > 0
  for i : locatedFoePos
    ?radarPts[i] = "[color=#red]ʘ[/color]"
      radarPts2[i] = "[color=#red]…[/color]"
    :
      radarPts[i] = "[color=#red]ʘ[/color]"
  radarMap2.text = string.Join("", radarPts2)
  radarMap.text = string.Join("", radarPts)
  radarPts2 = []
  for i = 0..49
    radarPts2.Add(" ")
  radarPts = string.Break("..................................................", 1)
:
  radarMap2.text = "                                                  "
  radarMap.text = ".................................................."

var markOnRuler
markOnRuler = "                                                  "
var rulerBits1 = ""
var rulerBits2 = ""
var markInterval
markInterval = math.Max(1, math.FloorToInt((50 * 5 + radarRange / 2) / radarRange))
for i = 0..49
  ?(i + 1) % markInterval = 0
    ?((i + 1) / markInterval) % 2 = 0
      rulerBits1 = string.Sub(markOnRuler, 0, i)
      ?i < 49
        rulerBits2 = string.Sub(markOnRuler, i+1, 49 - (i+1))
      markOnRuler = rulerBits1 + "'" + rulerBits2
    :
      rulerBits1 = string.Sub(markOnRuler, 0, i)
      ?i < 49
        rulerBits2 = string.Sub(markOnRuler, i+1, 49 - (i+1))
      markOnRuler = rulerBits1 + "5" + rulerBits2
radarMapRuler.text = markOnRuler

// Reset variables
rulerBits1 = ""
rulerBits2 = ""



// ----------------------------------- Player and foe info ----------------------------------- //

// ----- Variables ----- //
// Foe color
var foeclr
?loc.stars > 15
  foeclr = "#green"
:?loc.stars > 10
  foeclr = "#yellow"
:?loc.stars > 5
  foeclr = "#cyan"
:
  foeclr = "#white"

// Foe hp & name
var w = screen.w - 1
var h = screen.h - 1
var foeDist
var foeArmor
var foeHP
var foeName
foeName = element+" "+foe.name+" "+element
foeHP = foe.hp + "/" + foe.maxhp + " "
?foe.armor > 0
  foeArmor = "["+foe.armor+".0] "
:
  foeArmor = ""
foeDist = foe.distance


// ----- Print ----- //
// Player HP (bottom left)
>`1,@h@,#white,\O/ @hp@/@maxhp@
?armor + armor.f*0.1 >= 0.0
  >`@string.Size(""+hp+maxhp)+6@,
  ^@h@,#a9a9a9,[@armor@.@armor.f@]

// Enemy dist, count, HP, name (bottom right)
>`@w/3-7@,@h@,#yellow,Dist: @foe.distance@
^                                       
>`@w/3+4@,@h@,#magenta,Count: @foe.count@
?foe
  >`@w-string.Size(foeName+foeHP+foeArmor)@,@h@,
  ^#a9a9a9,@foeArmor@
  >`@w-string.Size(foeName+foeHP)@,@h@,
  ^#white,@foeHP@
  >`@w-string.Size(foeName)@,@h@,
  ^@foeclr@,@foeName@
:
  >`@w/3+15@,@h@,                              



// ------------------------------------------- Time ------------------------------------------- //

// ----- Store data ----- //
// Location star color
var locColor
?loc.stars <= 5
  locColor = "#ffffff"
:?loc.stars <= 10
  locColor = "#00ffff"
:?loc.stars <= 15
  locColor = "#ffff00"
:?loc.stars <= 20
  locColor = "#00ff00"
:?loc.stars <= 25
  locColor = "#0000ff"
:?loc.stars <= 30
  locColor = "#ff0000"
:
  locColor = "#rainff"

// Loop count tracker
var loopcount = 0
?loc.loop
  loopcount += 1

// Time data
var locTextGhost = "[color=" + locColor + "]" + loc.name + " *" + loc.stars + "* [/color]\n"
?string.Size(locTextGhost) > 42
  locTextGhost = "[color=" + locColor + "]..." + string.Sub(locTextGhost, string.Size(locTextGhost)-26, 26)
var timeTextGhost
timeTextGhost =
  ^"[color=#00cccc]  Screen.i: " + screen.i + "[/color]\n" +
  ^"[color=#cccc00]  Time (f): " + time + "f[/color]\n" +
  ^"[color=#cccc00] Frame (f): " + totaltime + "f[/color]\n" +
  ^"[color=#eeeeee]Total time: " + math.FloorToint(totaltime/30/60) + "m " + totaltime/30%60 + "s[/color]\n" +
  ^"[color=#b9b9ff] Avg. time: " + math.FloorToint(loc.averageTime/30/60) + "m " + loc.averageTime/30%60 + "s[/color]\n" +
  ^"[color=#8e8ed5] Best time: " + math.FloorToint(loc.bestTime/30/60) + "m " + loc.bestTime/30%60 + "s[/color]\n" +
  ^"[color=#a6a6a6]     Loops: " + loopcount + "[/color]"


// ----- UI elements ----- //
var timePanel
var timeToggleButton
var locText
var timeText

?loc.begin | loc.loop
  timePanel = ui.AddPanel()
  SetUI(timePanel, top_left, 20, 10, 0, 6)
  timePanel.color = "#cccc00"
  timePanel.visible = false
  drag.Add(timePanel)

  locText = ui.AddText()
  SetUI(locText, top_center, 18, 7, 0, 1)
  locText.text = locTextGhost
  locText.align = center
  timePanel.Add(locText)
  
  timeText = ui.AddText()
  SetUI(timeText, center_center, 18, 7, 0, 0)
  timePanel.Add(timeText)

  timeToggleButton = ui.AddButton()
  SetUI(timeToggleButton, bottom_right, 5, 3, -24, -1)
  timeToggleButton.text = "↑"
  timeToggleButton.bcolor = "#cccc00"
  timeToggleButton.tcolor = "#cccc00"
  timeToggleButton.SetPressed(TogglePanel)

?loc.begin
  timePanel.visible = false
var timePanel_isVisible = timePanel.visible
?loc.loop
  timePanel.visible = timePanel_isVisible
:
  timePanel_isVisible = timePanel.visible

timeText.text = timeTextGhost


// ----- Old prints ----- //
/* var locPrint
locPrint = ">> " + loc.name + " *" + loc.stars + " <<"
>`@screen.w/2-string.Size(locPrint)/2@,@screen.h-3@,@foeclr@,@locPrint@ */

// >`0,@screen.h-9@,@foeclr@,> @loc.name@ *@loc.stars@ <
// >`0,@screen.h-8@,#00cccc,  Screen.i: @screen.i@
// >`0,@screen.h-7@,#cccc00,  Time (f): @time@f
// >`0,@screen.h-6@,#cccc00,     Frame: @totaltime@f
// >`0,@screen.h-5@,#eeeeee,Total time:
// ^ @math.FloorToint(totaltime/30/60)@m
// ^ @totaltime/30%60@s
// >`0,@screen.h-4@,#b9b9ff, Avg. time:
// ^ @math.FloorToint(loc.averageTime/30/60)@m
// ^ @loc.averageTime/30%60@s
// >`0,@screen.h-3@,#8e8ed5, Best time:
// ^ @math.FloorToint(loc.bestTime/30/60)@m
// ^ @loc.bestTime/30%60@s
// >`0,@screen.h-2@,#a6a6a6,     Loops: @loopcount@
